ClearAll

// Fonte ideal
New Circuit.Thevenin bus1=A pu=1.05 basekv=13.8 model=ideal

// Transformador ideal
New Transformer.TrafoTri phases=3 windings=2 xhl=0.00000001
~ %loadloss=0.00000001 %noloadloss=0.00000001
~ wdg=1 bus=A.1.2.3 kV=13.8 kva=1 conn=delta
~ wdg=2 bus=B.1.2.3.4 kV=0.22 kva=1 conn=wye

// Resistência de aterramento
New Reactor.R phases=1 bus1=B.4 bus2=B.0 r=15 x=0

// Condutor 2 AWG
New Wiredata.2AWG
~ GMR=0.00269 DIAM=0.741 RAC=0.975 normamps=190
~ GMRunits=m RADunits=cm Runits=km

// Condutor 4 AWG
New Wiredata.4AWG
~ GMR=0.00213 DIAM=0.588 RAC=1.551 normamps=140
~ GMRunits=m RADunits=cm Runits=km

// Estrutura da rede - S1 - 2 AWG
New Linegeometry.S1_2AWG nconds=4 nphases=3 reduce=No
~ cond=1 wire=2AWG X=0.2 h=7.0 units=m
~ cond=2 wire=2AWG X=0.2 h=6.8 units=m
~ cond=3 wire=2AWG X=0.2 h=6.6 units=m
~ cond=4 wire=4AWG X=0.2 h=7.2 units=m

// Segmento de baixa tensão (SBT) - Caso ‘v’
New Line.L bus1=B.1.2.3.4 bus2=C.1.2.3.4 geometry=S1_2AWG
earthmodel=Carson length=0.1 units=km

// Carga representada por fonte de corrente
New Isource.ia amps=-140 phases=1 bus1=C.1 bus2=C.4 angle=0
New Isource.ib amps=-140 phases=1 bus1=C.2 bus2=C.4 angle=-120
New Isource.ic amps=-140 phases=1 bus1=C.3 bus2=C.4 angle=120

Solve

Show Losses